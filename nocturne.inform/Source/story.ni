"Nocturne" by Ruby Pink

[
TODO:
- find a unisex word for 80+ arousal
- once flora's horny level exceeds X, she mumbles your name in her sleep
- add achievements
- Change "Flora (verbs) yourself" to "Flora (verbs) you"
    - "[The actor] [touch] [us]."
- Better organization of actions & rules

CLOTHING:
- Before inserting, check if body part is covered
  - see section 2 clothing: https://bit.ly/3dwwDF2
- describe what flora is wearing so you can strip her
- clothing modifies interactions (less arousal through clothing? no insertion?)

EASTER EGGS:
- Magic words trigger gender swap?
- Add a "smell finger" easter egg
]

[======================================]

Volume 1 - Setup

The story headline is "An erotic adventure".
The story genre is "Erotica".
The release number is 0.
The story description is "TODO: An erotic adventure game."
The story creation year is 2022.

When play begins, say "You wake up suddenly in the middle of the night. You can hear your stepmom, Jo, showering. She works long hours on the swing shift to support you and your stepsister, Flora. It's been hard on your family since the divorce, but Jo has done everything she could to ensure that your and Flora's high school years were stable. Since graduation, Flora has been talking about college and moving out, but she's still here and so are you.

Thinking about the women in your life makes you aware that you are uncomfortably aroused. More than a typical nocturnal erection, your cock is throbbing as you think about how Flora walks around the house wearing only a t-shirt and panties. Your cock twitches as you remember the way her small dark nipples were visible through the thin fabric. You imagine pulling down her panties and rubbing…

You shake your head, trying to clear your mind. You know you shouldn't think of her like that. But soon the sound of the shower reminds you that Jo is naked and wet in the next room. You wrap your hand around your shaft as you imagine her soaping up her enormous breasts. You picture your cock nestled between her tits, pinching her nipples until she moans and begs you…

Abruptly, you throw the covers off and stand up in the dim moonlight. Your cock has never been so hard, and you know that jerking off isn't going to do the trick. You need something more… but what?"

Use American dialect and the serial comma.

[--------------------------------------]

Book 1 - Anatomy

[Note: some of these should be part of every person, but we say they're part of every woman because it's annoying to have to specify if you say "pinch nipple" and the game asks if you mean your nipple or Flora's nipple.]

A body part is a kind of thing.

A body part can be fuckable. A body part is usually not fuckable.
A body part can be openable. A body part is usually not openable.

A face is a kind of body part. A face is part of every woman. Understand "cheeks" and "nose" and "eyes" and "chin" as a face.

A mouth is a kind of body part. A mouth is part of every woman. A mouth is openable. A mouth is fuckable. Understand "lips" and "tongue" and "teeth" and "tonsils" and "throat" as a mouth.

A breast is a kind of body part. A breast is part of every woman. A breast is fuckable. Understand "boob" and "tit" as a breast. Understand "chest" and "boobs" and "tits" as the plural of breast.

A nipple is a kind of body part. A nipple is part of every woman. Understand "nip" as a nipple. Understand "nips" as the plural of nipple.

A clit is a kind of body part. A clit is part of every woman. Understand "clitoris" as a clit.

A pussy is a kind of body part. A pussy is part of every woman. A pussy is openable. A pussy is fuckable. Understand "vagina" and "cunt" and "lips" and "slit" as a pussy.

An ass is a kind of body part. An ass is part of every woman. An ass is openable. An ass is fuckable. Understand "butt" and "bottom" and "booty" and "butthole" and "asshole" as an ass.

A leg is a kind of body part. A leg is part of every woman. A leg is openable. Understand "thigh" and "calf" and "knee" as a leg. Understand "thighs" and "calves" and "knees" as the plural of leg.

A cock is a kind of body part. A cock is part of every man. Understand "penis" and "dick" and "member" and "erect" and "erection" and "hard-on" and "hard on" and "hardon" and "boner" and "stiffy" and "stiffy" and "shaft" as a cock. The description is usually "Your purple-headed warrior stands erect and ready to make you proud!"

A finger is a kind of body part. A finger is part of every person. The description is "digital."

[--------------------------------------]

Book 2 - Clothing

A garment is a kind of thing. A garment is always wearable.

Part 1 - Coverage Categories

An undergarment and a top and a bottom and a coverall are a kind of garment.

Part 2 - Garment Types (inherits coverage)

A dress and a robe and a jumpsuit are kinds of coveralls.

A shirt and a jacket are kinds of tops.

A skirt and pants are kinds of bottoms. The plural of pants is pants.

A bra and underwear are kinds of undergarments. The plural of underwear is underwear.

[--------------------------------------]

Book 3 - Conditions

Part 1 - Arousal

A person has a number called arousal.

Definition: a person is uninterested if their arousal is less than 20.
Definition: a person is interested if their arousal is at least 20.
Definition: a person is aroused if their arousal is at least 40.
Definition: a person is excited if their arousal is at least 60.
Definition: a woman is dripping if their arousal is at least 80.
Definition: a man is throbbing if their arousal is at least 80.
Definition: a person is orgasmic if their arousal is at least 100.

Part 2 - Alertness

A person has a number called alertness. The alertness of a person is usually 100.

Definition: a person is asleep if their alertness is less than 25.
Definition: a person is drowsy if their alertness is at least 25.
Definition: a person is alert if their alertness is at least 50.

[--------------------------------------]

Book 4 - Actions

Part 1 - Synonyms for Existing Actions

Understand the command "spread" as "open".
Understand the command "raise" or "lift" as "pull".
Understand the command "lower" as "push".
Understand the command "place" or "stick" or "shove" or "stuff" as "put".
Understand the command "stroke" as "rub".
Understand the command "strip" or "undress" as "shed".
Understand the command "pinch" as "squeeze".
Understand the command "poke" and "caress" and "tease" as "touch".
Understand the command "use" as "put". [use X on Y]

Part 2 - Extend Existing Actions

[Define some existing commands to be checked as sexual]
Eating a body part is performing sexual behavior.
Drinking a body part is performing sexual behavior.
Kissing is performing sexual behavior.
Inserting something into is performing sexual behavior.
Opening a body part is performing sexual behavior.
Pulling a body part is performing sexual behavior.
Rubbing is performing sexual behavior.
Smelling a body part is performing sexual behavior.
Squeezing is performing sexual behavior.
Tasting a body part is performing sexual behavior.
Touching is performing sexual behavior.
Turning a body part is performing sexual behavior.

[hold normally means "take"]
Understand "hold [body part]" as hugging.
Understand "hold [someone]" as hugging.

[put cock between tits]
Understand "put [something] between [something]" as inserting it into.

[allow kissing things as well as people]
Understand "kiss [something]" as kissing.

[allow stripping people as well as things]
Understand "shed [someone]" as taking off.

[add phrases the player might try to say]
Understand "you're dreaming" or "go back to sleep" as sleeping.

["tell flora she's dreaming"]
Understand "tell [someone] [text]" as telling it about.

["talk dirty to flora"]
Understand "talk [text] to [someone]" as telling it about (with nouns reversed).

["flora, touch your clit"]
Understand "your" or "yours" as a thing when the item described is enclosed by the person asked.

Part 3 - Override Existing Actions

[remap embrace and hug from kissing to new hugging action]
Understand the command "embrace" as something new.
Understand the command "hug" as something new.

[remap screw from turning to fucking]
Understand the command "screw" as something new.

Understand "man" or "gentleman" or "guy" or "dude" or "boy" as a man. Understand "girl" or "gal" or "woman" or "lady" as a woman.

Part 4 - Easter Eggs

Understand "plugh" or "xyzzy" or "frotz" or "plover" as a mistake ("TODO: What [we] do isn't magic. It's science.").

Part 5 - Update Existing Actions

To kiss is a verb.
Report an actor kissing:
	say "[The actor] [kiss] [the noun]."

Report an actor squeezing (this is the updated report squeezing rule):
	say "[The actor] [squeeze] [the noun]."
The updated report squeezing rule substitutes for the report squeezing rule.

Report an actor touching (this is the updated report touching things rule):
	say "[The actor] [touch] [the noun]."
The updated report touching things rule substitutes for the report touching things rule.

Report an actor turning (this is the updated report turning rule):
	say "[The actor] [turn] [the noun]."
The updated report turning rule substitutes for the report turning rule.

Report an actor pulling a body part:
	say "[The actor] [pull] [the noun]."

Instead of looking under a garment:
	say "[The actor] [look] under [the noun]."

To raise is a verb.
Instead of pulling a garment:
	say "[The actor] [raise] [the noun]."

To lower is a verb.
Instead of pushing a garment:
	say "[The actor] lower [the noun]."

Instead of opening:
	if the noun is a person:
		if the noun incorporates a pussy (called her pussy):
			try opening her pussy instead;
	else if the noun is a leg:
		say "You open [the noun][s].";
		rule succeeds;
	else if the noun is openable:
		say "You open [the noun].";
		rule succeeds;
	else:
		continue the action;

Instead of inserting:
	if the second noun is a person:
		if the second noun incorporates a pussy (called her pussy):
			try the actor inserting the noun into her pussy instead;
	else if the second noun is a breast:
		say "You put [the noun] between [the second noun][s].";
		rule succeeds;
	else if the second noun is fuckable:
		say "You put [the noun] in [the second noun].";
		rule succeeds;
	else:
		continue the action;

Instead of eating:
	if the noun is a pussy or the noun is an ass:
		say "You eat [the noun].";
	else:
		continue the action;

Check an actor drinking something:
	if the noun is a body part:
		try the actor sucking the noun instead;
	else:
		continue the action;

Instead of entering:
	if the noun is a person:
		try fucking the noun instead;
		
Instead of telling someone about "dirty":
	try encouraging the noun instead;
		
Instead of telling someone about "she's dreaming":
	try asking the noun to try sleeping;

Instead of answering someone that "go down on me":
	if the actor incorporates a cock (called his cock):
		try asking the noun to try sucking his cock instead;
	else if the actor incorporates a clit (called her clit):
		try asking the noun to try licking her clit instead;

Instead of answering someone that "make me cum":
	try the actor cumming;

Check an actor sleeping (this is the updated block sleeping rule):
	if the actor is the player:
		say "[We] [aren't] feeling especially drowsy." (A);
		stop the action;
	else:
		if actor is alert:
			say "[The actor] is too alert to go back to sleep.";
			rule succeeds;
		else:
			try reassuring the actor instead.
The updated block sleeping rule substitutes for the block sleeping rule.

Instead of an actor putting something on:
	if the second noun is a person:
		if the second noun incorporates a clit (called her clit):
			try the actor touching her clit with the noun instead;
	else if the second noun is a body part:
		try the actor touching the second noun with the noun instead;

To twist is a verb.
Check an actor turning a nipple:
	say "[The actor] [twist] [the noun].";
	rule succeeds;
	
The requested actions require persuasion rule response (A) is "[The noun] [if the noun is not alert]is too drowsy to respond to your request[else][have] better things to do[end if]."

Part 6 - New Actions

To bite is a verb.
Biting is an action applying to one thing.
Biting a body part is performing sexual behavior.
Biting someone is performing sexual behavior.
Understand "bite [something]" as biting.
Report an actor biting:
	say "[The actor] [bite] [the noun]."

To cum is a verb.
Cumming is an action applying to nothing.
Cumming is performing sexual behavior.
Understand "cum" and "cum for me" as cumming.
Understand the command "come" and "orgasm" and "jizz" and "squirt" as "cum".
Report an actor cumming:
	say "[The actor] [cum]."

Cumming in is an action applying to one thing.
Cumming in is performing sexual behavior.
Understand "cum in [something]" as cumming in.
Report an actor cumming in:
	say "[The actor] [cum] in [the noun]."

Cumming on is an action applying to one thing.
Cumming on is performing sexual behavior.
Understand "cum on [something]" as cumming on.
Report an actor cumming on:
	say "[The actor] [cum] on [the noun]."

Encouraging is an action applying to one thing.
Understand "encourage [someone]" as encouraging.
Understand the command "excite" as "encourage".
Check an actor encouraging:
	increase the arousal of the noun by 5;
Report an actor encouraging:
	say "You whisper dirty little nothings in [the noun]'s ear, getting her hot.";

To finger is a verb.
Fingering is an action applying to one thing.
Fingering is performing sexual behavior.
Understand "finger [something]" as fingering.
Understand the command "fingerbang" and "fingerfuck" as "finger".
Check an actor fingering someone:
	if the noun incorporates a pussy (called her pussy):
		try the actor fingering her pussy instead;
Report an actor fingering:
	say "[The actor] [finger] [the noun]."

To fuck is a verb.
Fucking is an action applying to one thing.
Fucking is performing sexual behavior.
Understand "fuck [something]" as fucking.
Understand the command "screw" and "bang" as "fuck".
Report an actor fucking:
	say "[The actor] [fuck] [the noun]."

Fucking it with is an action applying to two things.
Fucking something with is performing sexual behavior.
Understand "fuck [other things] with [something]" as fucking it with.
Check an actor fucking something with:
	if the noun is a person:
		if the noun incorporates a pussy (called her pussy):
			try the actor inserting the second noun into her pussy instead;
	else if the noun is a body part:
		if the noun is fuckable:
			try the actor inserting the second noun into the noun instead;
		else:
			try the actor rubbing the noun with the second noun instead;

To hug is a verb.
Hugging is an action applying to one thing.
Hugging a body part is performing sexual behavior.
Understand "hug [something]" as hugging.
Understand the command "cuddle" and "embrace" as "hug".
Report an actor hugging:
	say "[The actor] [hug] [the noun]."

To lick is a verb.
Licking is an action applying to one thing.
Licking a body part is performing sexual behavior.
Licking someone is performing sexual behavior.
Understand "lick [something]" as licking.
Report an actor licking:
	say "[The actor] [lick] [the noun]."

Masturbating is an action applying to nothing.
Masturbating is performing sexual behavior.
Understand "masturbate" and "jerk off" and "rub one out" as masturbating.
Check an actor masturbating:
	if the actor incorporates a cock (called his cock):
		try the actor trying rubbing his cock instead;
	else if the actor incorporates a clit (called her clit):
		try the actor trying rubbing her clit instead;

Reassuring is an action applying to one thing.
Understand "reassure [someone]" as reassuring.
Check an actor reassuring:
	if the alertness of the noun is at least 5:
		decrease the alertness of the noun by 5;
	else:
		now the alertness of the noun is 0;
Report an actor reassuring:
	if the actor is alert:
		say "You whisper reassuring things in [the noun]'s ear. It seems to settle them a bit.";
	else:
		say "Worried she's starting to wake up, you whisper reassuring things in [the noun]'s ear. It seems to help, and she breathes a bit deeper.";

Rubbing it with is an action applying to two things.
Rubbing something with is performing sexual behavior.
Understand "rub [other things] with [something]" as rubbing it with.
Check an actor rubbing something with:
	if the noun is a person:
		if the noun incorporates a clit (called her clit):
			try the actor rubbing her clit with the second noun instead.
Report an actor rubbing something with:
	say "[The actor] [rub] [the noun] with [the second noun]."

To spank is a verb.
Spanking is an action applying to one thing.
Spanking a body part is performing sexual behavior.
Spanking someone is performing sexual behavior.
Understand "spank [something]" as spanking.
Understand the command "slap" or "smack" as "spank".
Check an actor spanking someone:
	if the noun incorporates an ass (called their ass):
		try the actor spanking their ass instead.
Report an actor spanking:
	say "[The actor] [spank] [the noun]."

To spit is a verb.
Spitting on is an action applying to one thing.
Spitting on a body part is performing sexual behavior.
Spitting on someone is performing sexual behavior.
Understand "spit on [something]" as spitting on.
Report an actor spitting on:
	say "[The actor] [spit] on [the noun]."

To suck is a verb.
Sucking is an action applying to one thing.
Sucking a body part is performing sexual behavior.
Sucking someone is performing sexual behavior.
Understand "suck [something]" and "suck on [something]" as sucking.
Understand the command "blow" as "suck".
Check an actor sucking:
	if the noun is a cock incorporated by the player:
		if the actor is the player:
			say "[suck your own dick]" instead;
	if the noun is the player:
		if the actor is the player:
			say "[suck your own dick]" instead;
		else:
			try the actor sucking your cock instead;
Report an actor sucking:
	say "[The actor] [suck] [the noun]."
To say suck your own dick:
	say "You know, my cousin Walter broke his neck trying to suck his own dick. I swear, bible truth. My aunt found him. It was a mess. He was on his bed, with his legs doubled over himself. My aunt freaked out. He had his dick in his mouth, balls resting on his lips. He really made it. But at what a price?"

Touching it with is an action applying to two things.
Touching something with is performing sexual behavior.
Understand "touch [other things] with [something]" as touching it with.
Check an actor touching something with:
	if the noun is a person:
		if the noun incorporates a clit (called her clit):
			try the actor touching her clit with the second noun instead.
Report an actor touching something with:
	say "[The actor] [touch] [the noun] with [the second noun]."

Part 7 - Debugging - Not for release

Arousing is an action applying to one thing and one number. Understand "arouse [someone] to [number]" as arousing.

Carry out arousing:
	now the arousal of the noun is the number understood;
	say "[The noun]'s arousal is now [the number understood]."

Alerting is an action applying to one thing and one number. Understand "alert [someone] to [number]" as alerting.

Carry out alerting:
	now the alertness of the noun is the number understood;
	say "[The noun]'s alertness is now [the number understood]."

Debugging is an action applying to one thing. Understand "debug [someone]" as debugging.

Carry out debugging:
	say "DEBUG: Alertness [the alertness of the noun]; ";
	if the noun is alert:
		say "[The noun] [are] alert.";
	else if the noun is drowsy:
		say "[The noun] [are] drowsy.";
	else if the noun is asleep:
		say "[The noun] [are] asleep.";
	say "DEBUG: Arousal [the arousal of the noun]; ";
	if the noun is orgasmic:
		say "[The noun] [are] cumming hard!!";
	else if the noun is throbbing:
		say "[The noun] [are] throbbing!";
	else if the noun is dripping:
		say "[The noun] [are] dripping wet!";
	else if the noun is excited:
		say "[The noun] [are] excited!";
	else if the noun is aroused:
		say "[The noun] [are] aroused.";
	else if the noun is interested:
		say "[The noun] [are] interested.";
	else if the noun is uninterested:
		say "[The noun] [are] uninterested.";

Before examining someone:
	try debugging the noun;

When play begins (this is the run property checks at the start of play rule):
	repeat with item running through things:
		unless the item is part of Alice:
			if description of the item is "":
				say "[item] has no description."

Part 8 - Testing

test alertness with "reassure her / debug her / tell her she's dreaming / debug her / flora, you're dreaming / debug her / flora, go back to sleep / debug her".

test arousal with "encourage her / debug her / talk dirty to her / debug her".

test reset with "alert her to 0 / arouse her to 100".

test her with "spank her / kiss her / rub her / squeeze her / touch her / bite her / cum on her / hold her / lick her / spit on her / suck her".

test boob with "spank boob / kiss boob / rub boob / squeeze boob / touch boob / bite boob / cum on boob / hold boob / lick boob / spit on boob / suck boob".

test nipple with "touch her nipple / pinch her nipple / pull her nipple / twist her nipple / suck her nipple / bite her nipple".

test vibrator with "purloin vibrator / touch clit with vibrator / touch pussy with vibrator / touch her with vibrator / rub clit with vibrator / rub pussy with vibrator / rub her with vibrator / put vibrator on clit / put vibrator on pussy / put vibrator on her / use vibrator on clit / use vibrator on pussy / use vibrator on her / fuck her clit with vibrator / fuck her pussy with vibrator / fuck her with vibrator".

test inserting with "purloin purple dildo / insert cock in her / insert cock into pussy / stick cock in her ass / stuff cock inside her mouth / put cock between boobs / insert dildo in her / insert dildo into pussy / stick dildo in her ass / stuff dildo inside her mouth / put dildo between boobs"

test undressing with "take off bra / take off shirt / look under bra / raise bra / strip bra / look under panties / lower panties / undress her".

test instructions with "flora, swallow my cock / flora, lick my cock / flora, blow me / flora, go down on me / flora, rub my cock / flora, fuck me / flora, rub one out / flora, make me cum / flora, cum for me".

test opening with "spread her / open legs / spread pussy / spread her ass / open her mouth"

test foreplay with "talk dirty to her / spread pussy / smell pussy / taste pussy / lick pussy / eat pussy / finger pussy / fuck pussy".

test fucking with "spread her / finger her / enter her / fuck her".

test masturbation with "stroke cock / rub cock / masturbate / jerk off".

test cumming with "cum / jizz on her / squirt in her / cum for me".

test body with "x her / x her face / x her mouth / x her breast / x her nipple / x her clit / x her pussy / x her ass"

[--------------------------------------]

Book 5 - Rules

Part 1 - Disable Core Rules

The can't take people's possessions rule does nothing when the actor is the player.
The can't take off what's not worn rule does nothing when the actor is the player.
The can't remove from people rule does nothing when the actor is the player.
The block giving rule is not listed in the check giving it to rules.
The block attacking rule does nothing when the noun is a body part.
The block kissing rule is not listed in the check kissing rules.
The innuendo about squeezing people rule is not listed in the check squeezing rules.
The report touching yourself rule does nothing when the actor is the player.
The report touching things rule does nothing when the actor is the player.
The report touching other people rule does nothing when the actor is the player.
The the can't rub another person rule is not listed in the check rubbing rules.
The report pulling rule does nothing when the noun is a body part.
The can't put onto what's not a supporter rule does nothing when the second noun is a body part.
The can't put onto what's not a supporter rule does nothing when the second noun is a person.

Part 2 - Persuasion Rules

[horny people are willing to do stuff]
Persuasion rule for asking an aroused person to try doing something:
	persuasion succeeds.

Persuasion rule for asking someone to try sleeping:
	persuasion succeeds.

Part 3 - Scenery Rules

[better messages when taking scenery]
Instead of taking anything that is scenery:
	say "You don't need that right now."

[better messages when moving scenery]
Instead of pushing or pulling anything that is scenery:
	say "You don't need to move that right now."

Part 4 - Status Check Rules

[Note: These are Instead rules so they can be overidden by more specific Before rules for individual NPCs. They can't be Check rules because they apply to multiple actions.]

[don't touch alert people]
Instead of performing sexual behavior (this is the don't touch alert people rule):
	if the noun is a body part that is part of someone (called the owner) that is not the player and the owner is alert and the owner is not aroused:
		say "Touching [the noun] while [if the owner is female]she[else]he[end if] is awake would be too awkward!";
	else if the second noun is a body part that is part of someone (called the owner) that is not the player and the owner is alert and the owner is not aroused:
		say "Touching [the second noun] while [if the owner is female]she[else]he[end if] is awake would be too awkward!";
	else if the noun is a person that is not the player and the noun is alert and the noun is not aroused:
		say "Touching [the noun] while [if the noun is female]she[else]he[end if] is awake would be too awkward!";
	else:
		continue the action;

[don't undress alert people]
Instead of taking a garment that is worn by someone (called the owner) that is not the player (this is the don't undress alert people rule):
	if the owner is alert and the owner is not aroused:
		say "Trying to remove [the noun] while [printed name of owner] is awake would be too awkward!";
	else:
		continue the action;

Part 5 - Undressing Rules

[can't take a bra from under a shirt]
Instead of an actor taking or taking off a bra worn by someone (called the owner) (this is the check if the bra is covered rule):
	if the owner is wearing a top or the owner is wearing a coverall:
		say "[The actor] can't take off [if the owner is the player]your[else if the owner is female]her[else]his[end if] bra while [if the owner is female]she[else]he[end if] is wearing something over it.";	
	else:
		continue the action;

[can't take underwear from under pants]
Instead of an actor taking or taking off underwear worn by someone (called the owner) (this is the check if the underwear is covered rule):
	if the owner is wearing a bottom or the owner is wearing a coverall:
		say "[The actor] can't take off [if the owner is the player]your[else if the owner is female]her[else]his[end if] underwear while [if the owner is female]she[else]he[end if] is wearing something over it.";
	else:
		continue the action;

[when strip targets a person, remove a random garment]
Check taking off somebody (this is the stripping a person rule):
	if the noun is wearing something:
		let random-garment be a random thing worn by the noun;
		say "[The noun] is wearing [a list of things worn by the noun]. You decide to remove [random-garment].";
		try taking off random-garment instead;
	otherwise:
		say "But [the noun] [are] already naked!" instead;

[======================================]

Volume 2 - Rooms

Book 1 - The House

The House is a region.

A light switch is a kind of scenery device. A light switch is in every room. The description is usually "Off? On? This light switch does it all."

Instead of switching on or switching off a light switch, say "Better not, you don't want anyone to notice you."

[--------------------------------------]

Part 1 - Your Room

Your Room is in the house and east of the hallway. "The moonlight reveals a messy room. Your bed is unmade, and there are dirty clothes scattered around the floor. Posters of scantily-clad women cover the walls. There is a box of tissues and a bottle of lotion next to the computer. To the west is the hallway."

Chapter 1 - Scenery

Your posters are scenery in your room. Understand "poster" as your posters. "A poster of [one of]a woman in a small bikini and torn tights carrying a massive sniper rifle[or]a supermodel in a bikini eating a cheeseburger[or]the lead female character from your favorite anime wearing skimpy lingerie[or]just a close-up shot of a woman's cleavage[then at random]."

Your computer is scenery in your room. "It's time to start saving up for a new computer. This one can't run any recent games. It's really only good for watching porn."

Your laundry is scenery in your room. Understand "dirty" and "clothes" as your laundry. "Dirty laundry, look out, I'm coming for you! *finger guns* Alright, don't worry about it, you're fine, you're fine."

The tissues are scenery in your room. Understand "box" and "kleenex" as the tissues. "Jo wonders why you go through so many tissues. You tell her you have allergies."

The lotion is scenery in your room. Understand "bottle" as the lotion. "What? You just like to moisturize."

Your bed is scenery in your room. "Precious treasures are buried beneath this bed."

Your socks are scenery in your room. understand "sock" as your socks. "These socks are crusty and gross."

Your treasure is scenery in your room. "A small collection of treasured porn mags." Understand "treasures" and "pornography" and "porn" and "magazines" and "mags" as your treasure.

Chapter 2 - Interactions

Instead of looking under your bed, say "Mostly socks, but also treasures."

Instead of doing anything other than examining with your socks, say "Ew!"

[--------------------------------------]

Part 2 - Hallway

The Hallway is a room in the house. "The light spilling into the hallway under the bathroom door is just enough to make out the family photos on the walls. To the east is your room, to the west is Jo's room, to the north is Flora's room, and to the northeast is the bathroom. Stairs lead down to the kitchen."

Chapter 1 - Scenery

The family photos are scenery in the hallway. "There are many photos in the hallway of your family. Since the divorce, Jo removed all the ones of your father. Those remaining show various combinations of you, Flora, and Jo."

A photo is scenery in the hallway. Understand "family photo" as a a photo. "This one shows [one of]Jo and Flora at the beach wearing bikinis. The difference between their bodies is astonishing. Jo's full body is practically spilling out of the tight black fabric, while Flora's breasts look modest in comparison, despite her nipples poking through the orange cups[or]you and Flora at the fair. She challenged you to a corndog eating contest and you watched, astonished as she practically deep-throated sausage after sausage[or]you and Jo in silly hats at a Christmas party. You remember struggling not to stare down the prominent cleavage of Jo's holiday dress[or]you with Jo and Flora at a family picnic. A lovely day together which is only slightly tainted by the memory of Jo catching you masturbating into Flora's dirty panties in the bathroom that night[then at random]."

[--------------------------------------]

Part 3 - Flora's Room

Flora's Room is in the house and north of the hallway. "The soft moonlight reveals a textbook example of a teenage girl's room. Posters of cute boys on the wall, stuffed animals and loads of pillows on the bed. Her desk is piled with marine biology textbooks. To the south is the hallway."

Chapter 1 - Scenery

Flora's posters are scenery in Flora's room. Understand "poster" as Flora's posters. "A poster of [one of]the members of Flora's favorite K-pop band, dressed up as cowboys for some reason[or]a humpback whale swimming in sun-dappled waters[or]Envy Adams, the lead singer of The Clash at Demonhead. Someone wearing lipstick planted a kiss in Envy's cleavage[or]an octopus lounging in front of some coral. The octopus also has a lipstick kiss[then at random]."

Flora's stuffed animals are scenery in Flora's room. Understand "bears" and "bear" and "teddy" as Flora's stuffed animals. "There's quite an assortment of stuffed animals piled at the foot of Flora's bed.[if the octopus is nowhere] Teddy bears piled haphazardly, almost like they're concealing something."

Flora's pillows are scenery in Flora's room. Understand "pillow" as Flora's pillows. "[if the cute vibrator is nowhere]You can see something under the pillow."

Flora's bed is scenery in Flora's room. "[if the Flora's diary is nowhere]You can see something under the bed."

Flora's desk is a scenery supporter in Flora's room. "Flora's desk is mostly used to store a collection of marine biology textbooks."

Flora's textbooks are scenery on Flora's desk. Understand "marine" and "biology" and "books" and "book" as Flora's textbooks. "This pile of marine biology textbook seems mind-numbingly dull to you, but Flora is obsessed with sea creatures."

Chapter 2 - Things

The octopus is a thing. Understand "strap-on" and "black" and "leather" and "harness" as the octopus. The printed name is "Octopus with a strap-on[unless the octopus incorporates the tentacle dildo] harness". The description is "This large stuffed octopus toy is wearing a black leather strap-on harness.[if the octopus incorporates the tentacle dildo] Mounted in the harness is a dildo shaped like a tentacle.[end if] Some stained spots make it clear that this homemade ride-on tentacle fucker has been well-loved. You can easily imagine Flora straddling it, tugging the cloth tentacles against her body, fantasizing about this sea creature ravishing her."

The tentacle dildo is a part of the octopus. The description is "This bright pink and purple dildo looks surprisingly realistic, with molded suckers and a bumpy texture."

The cute pink vibrator is a device. Understand "vibe" and "toy" as the vibrator. The description is "A vibrator with cute pink flower pattern."

Flora's diary is a thing. The description is "Holy shit. As you flip through Flora's diary, you discover she has been having increasingly more explicit sex dreams about you. She wavers between feeling guilty about these fantasies and noting that it's not like you're actually related."

A dream is a part of Flora's diary. The description is "In this dream, [one of]Flora went down on you. It started out sweet and gentle, but by the end you had a grip on her hair to force her to take you deeper, until she was gagging as you came down her throat. When she woke up, her panties were soaked[or]you caught Flora sneaking out, bent her over the kitchen table, lifted her skirt, pulled down her panties, and spanked her until she was dripping wet and couldn't walk straight[or]Flora caught you sniffing her panties, but instead of being mad, she let you go to the source. She lay you down and sat on your face, pressing her soaking wet pussy against your face, and let you lick her clit while she sucked your cock[or]you, Jo, and Flora were watching a movie. She noticed you had a hard-on, sat on your lap, slipped your cock inside her shorts, and fucked you quietly, hoping Jo wouldn't notice[or]Flora was riding her octopus, it's tentacles squeezing her tits, when you were suddenly behind her and started fucking her ass. Being filled in both holes made her cum again and again in the dream, and when she woke up, she had to use her vibrator[or]Flora was laying on her back, rubbing her clit, when a spotlight turned on, and she realized that she was being watched by you, Jo, Envy Adams, and her octopus. Everyone was rubbing one out and crawling closer, their faces approaching her spread thighs, until she squirted in all their faces[then at random]."

Chapter 3 - Interactions

Instead of pushing or pulling Flora's pillows:
	try looking under Flora's pillows.

Instead of looking under Flora's pillows for the first time:
	now the vibrator is in Flora's Room;
	say "A vibrator peeks out from under the pillow."

Instead of looking under Flora's bed for the first time:
	now Flora's diary is in Flora's Room;
	say "Just visible under the bed, you can see Flora's diary."

Instead of searching or pushing or pulling Flora's stuffed animals:
	try looking under Flora's stuffed animals.

Instead of looking under Flora's stuffed animals for the first time:
	now the octopus is in Flora's Room;
	say "What the fuck? Buried in the stuffed animal pile, you discover a large stuffed octopus with a strap-on!"

Instead of taking the tentacle dildo:
	if the dildo is part of the octopus:
		now the tentacle dildo is carried by the player;
		say "With some effort you extract the tentacle dildo from the octopus's strap-on harness, and leave the octopus on the stuffed animal pile."

Instead of taking the octopus:
	say "That's too big to carry around[if the octopus incorporates the tentacle dildo], but you could probably take the tentacle dildo?[otherwise]."

Instead of taking Flora's diary:
	say "Better leave it here, if Flora found it missing, she would accuse you immediately."

[--------------------------------------]

Part 4 - Bathroom

The Bathroom is in the house and northeast of the hallway. "The light in your family's shared bathroom seems dazzlingly bright compared to the hallway. A dirty laundry basket sits in the corner.[if someone is in the shower] Steam from the shower has fogged up the mirror.[end if] The hallway is to the southwest."

Chapter 1 - Scenery

The dirty laundry basket is scenery in the bathroom. "The dirty laundry basket is filled to the brim with bras and panties. Why are women's underthings so much nicer than men's?"

The bra pile is scenery in the bathroom. The printed name is "bras and panties". Understand "bras" and "bra" and "panties" and "underwear" as the bra pile. "You grab one at random. This one is [one of]covered in cute little fish, must be Flora's[or]red satin[or]frilly and lacy[or]see-through black mesh[or]strappy and scandalous, must be Jo's[then at random]. After a moment, you put it back in the basket."

Some fixtures are scenery in the bathroom. Understand "sink", "toilet", "faucet", "mirror", "cabinet", "tub", "basin", "towel", "bath", and "mat" as the fixtures. The description is "The bathroom fixtures are not very interesting."

Chapter 2 - Things

The shower is an enterable scenery container in the bathroom. Understand "curtain" as the shower. The description is "[if someone is in the shower]The shower is running.[otherwise]The shower is turned off."

Chapter 3 - Interactions

Instead of smelling the bra pile, say "They smell kinda nice, but you put them down quickly, before you get caught again."

Instead of taking the bra pile, say "Oh no, not after what happened last time."

Instead of doing anything other than examining with the fixtures, say "You have more important things to do right now than fiddle with the bathroom fixtures."

[--------------------------------------]

Part 5 - Jo's Room

Jo's Room is in the house and west of the hallway. "Blackout curtains cover the windows. The dim light from the hallway is just enough to see a pile of romance novels has spilled off the nightstand and some have fallen under the bed. On the bed is an overstuffed body pillow. The hallway is to the east."

Chapter 1 - Scenery

Jo's blackout curtains are scenery in Jo's room. "Jo works odd hours and needs blackout curtains so she can sleep during the day."

Jo's bed is scenery in Jo's room. "[if the purple dildo is nowhere]You can see something under the bed."

Jo's pillow is scenery in Jo's room. "[if Jo's phone is nowhere]You can see a light glinting under the pillow."

Jo's nightstand is a closed openable scenery container in Jo's room. Understand "drawer" as Jo's nightstand. "The nightstand is covered in pile of paperback romance novels[if nightstand is closed and there is something in the nightstand]. You wonder what's inside the closed drawer[end if]."

Jo's romance novels are scenery in Jo's room. Understand "novel" and "paperback" as Jo's romance novels. "You glance at the cover of one of the paperbacks: [one of]'Emily’s Magical Bejewelled Codpiece.'[or]'Desert Prince, Defiant Virgin.'[or]'Apollo’s Seed.'[or]'Falling for the Highlander.'[or]'Husbands on Horseback.'[or]'Confessions of a Royal Bridegroom.'[then at random]".

Chapter 2 - Things

Jo's phone is a thing. The description is "Jo left her phone unlocked. Your eye is drawn to the photo gallery app."

The photo gallery app is a part of Jo's phone. The description is "You flip back through Jo's selfies, and find a series of shockingly explicit nude pics of your stepmom."

A selfie is a part of the photo gallery app. Understand "pic" and "snapshot" and "photo" and "nude" and "nudes" and "explicit" as a selfie. The description is "In this photo, Jo [one of]is sucking on one of her own nipples[or]is laying on her back with her legs crossed, a surprisingly full bush of dark pubic hair concealing her pussy[or]is deep throating a large purple dildo[or]has her legs spread, revealing her dripping wet lips and oversized clit[or]is biting her pillow and shoving four fingers into her pussy[or]is standing, spreading her ass to reveal the base of a metal butt plug[then at random]."

The bottle of lube is in Jo's nightstand. The description is "A bottle of lube could be useful."

The metal butt plug is in Jo's nightstand. The description is "This butt plug is made out of metal and surprisingly heavy."

The big purple dildo is a thing. The description is "A large, realistic-looking purple dildo."

Chapter 3 - Interactions

Instead of pushing or pulling Jo's pillow:
	try looking under Jo's pillow;

Instead of looking under Jo's pillow for the first time:
	now Jo's phone is in Jo's Room;
	say "Tucked under the pillow, you see Jo's phone, the screen still unlocked!"

Instead of looking under Jo's bed for the first time:
	now the purple dildo is in Jo's Room;
	say "Just visible under the bed, you can see a big purple dildo."

Instead of taking Jo's phone:
	say "Better leave it here, Jo would notice it missing right away."
	
Instead of taking a selfie:
	say "You quickly send the nudes to yourself for later use."

[--------------------------------------]

Part 6 - Kitchen

The Kitchen is in the house and down from the hallway. "A typical kitchen. Stairs go up to the hallway. Nothing to see here, yet."

[======================================]

Volume 3 - Characters

Book 1 - You

[The player is a person by default, which does not get man parts.]
[this phrasing is odd, but simpler than "Bob is a man. The player is Bob."]
The yourself is a man.

The description of the player is "TODO: In the absence of a mirror, you're not quite sure what you look like today."


[--------------------------------------]

Book 2 - Jo

Jo is a woman in the shower. "Jo is in the shower, lathering soap around her enormous tits. Without her glasses, she can't see very well. 'Is that you, Flora? Can you hand me towel, hon?'"

The description of Jo is "Your stepmother has a lovely smile and enormous tits with prominent nipples. Her brunette hair is tied up in a bun. She has a tidy bush of dark pubic hair."

Jo has arousal 80.

Part 1 - Body Parts

The description of Jo's breast is "Jo's enormous breasts hang heavy, glistening with water. Her firm nipples, each the size of your fingertip, stand proudly in the center of a dark brown areola."

The description of Jo's clit is "Unusually large, Jo's clit protrudes from its hood, above prominent lips, demanding attention."

Part 2 - Interactions

Instead of touching Jo's clit, say "[if Jo is aroused]Jo groans and you feel a gush of hot liquid over your fingers.[else]Not now, honey, I need to get to work."

[--------------------------------------]

Book 3 - Flora

Flora is a woman in your room. "Flora is asleep on her back. She's kicked the blanket off, and looks restless."

The description of Flora is "Your stepsister has a coy smile. Her wavy brunette hair hangs just past her shoulders, which, like her face, are covered in freckles. She is shorter than you, with a slim figure and small, perky breasts."

Flora has alertness 0. Flora has arousal 30.

Part 1 - Body Parts

The description of Flora's breast is "[flora-breast-description]".
The description of Flora's nipple is "[flora-breast-description]".

To say flora-breast-description:
	if flora is wearing a bra or flora is wearing a top or flora is wearing a coverall:
		say "You can't see [the noun]!";
	else:
		say "Flora's small breasts are crowned with nipples just a bit darker pink than the rosy color of her areola, which fade gently into the pale color of her soft flesh."

The description of Flora's clit is "[flora-clit-description]".

To say flora-clit-description:
	if flora is wearing underwear or flora is wearing a bottom or flora is wearing a coverall:
		say "You can't see [the noun]!";
	else:
		say "Flora's clit peeks demurely from its hiding place."

Part 2 - Clothing

Flora is wearing a bra called the polka-dot bra. The description of the bra is "Flora's bra is pink with white polka-dots."

Flora is wearing underwear called the polka-dot panties. The description of the underwear is "Flora's panties are pink with white polka-dots."

Flora is wearing a shirt called the henley shirt. The description of the shirt is "Flora's shirt is a comfortable old henley shirt with purple sleeves and a deep v-neck. The fabric is thin and leaves little to the imagination."

[
- attack: slap, smack, spank
- inserting it into: put it in, put it between, stick, stuff
- kissing
- rubbing: stroke
- squeezing: pinch
- touching: feel, poke, caress, tease
- turning: twist

- shed: strip, undress, take off garment
- look under
- pushing: lower, move, press, shift
- pulling: lift, raise

- enter
- open: spread
- close

- drinking: swallow
- eating
- smelling: sniff
- tasting

- biting
- cumming (in/on): orgasm, squirt
- fingerbanging: finger
- fucking: bang, screw
- hugging: cuddle, embrace
- licking
- masturbating: jerk off, rub one out
- spitting on
- sucking: blow

- face: cheeks, nose, eyes, chin
- mouth: lips, tongue, teeth, tonsils, throat
- breast: boob, tit, chest
- nipple: nip
- clit: clitoris
- pussy: pussy, cunt, lips
- ass: butt, bottom, booty, butthole, asshole
]

Part 3 - Interactions

[Check alertness before touching]
Before performing sexual behavior (this is the check if Flora is alert rule):
	if the noun is Flora or the noun is part of Flora:
		if Flora is alert and Flora is not aroused:
			say "Flora gasps in shock, and clamps her legs tightly together. 'What are you doing?' She angrily chases you out of her room.";
			now the player is in the Hallway instead;

Chapter 1 - Clothing

[after removing panties, express surprise that she's shaved]
After taking or taking off the polka-dot panties for the first time:
	say "After removing her panties, you are surprised to discover that Flora is fully shaved! Her bare pussy is now exposed."

Chapter 2 - Body Parts

Before opening Flora's leg:
	say "You spread Flora's legs wide, and revel in the sight of her pussy.";
	rule succeeds;

Before inserting:
	if the second noun is Flora's mouth:
		say "You stuff [the noun] into Flora's mouth.";
		rule succeeds;

Section 1 - Face

[face: cheeks, nose, eyes, chin]

Section 2 - Mouth

[mouth: lips, tongue, teeth, tonsils, throat]

Section 3 - Breasts

Instead of touching or rubbing Flora's breast:
	increment arousal of Flora;
	say "Flora giggles and swats your hand away.";

Section 4 - Nipples

Instead of touching or rubbing Flora's nipple:
	if Flora is excited:
		say "oh my!";
	increase the arousal of Flora by 20;
	say "Flora squirms as her nipple stiffens in your hand.";

Section 5 - Clit

Instead of touching or rubbing Flora's clit:
	if Flora is orgasmic:
		say "Flora cries out as she squirts.";
	else if Flora is dripping:
		say "Flora's pussy is dripping wet and she is panting as you bring her towards climax.";
	else if Flora is excited:
		say "Flora moans and squirms her hips as you touch her.";
	else if Flora is aroused:
		say "Flora squeals in delight.";
	else if Flora is interested:
		say "Flora frowns and stirs in her sleep. Don't go straight for the clit, bonehead, get her wet first!";
	else:
		now the alertness of Flora is 100;
		try touching Flora's clit;
		
Section 6 - Vagina

[pussy: pussy, cunt, lips]

Section 7 - Ass

[ass: butt, bottom, booty, butthole, asshole]

[--------------------------------------]

Book 4 - Alice - Not for release

Alice is a woman in the hallway. The description is "Alice is a generic woman for testing generic action responses on."
